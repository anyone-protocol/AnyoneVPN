apply from: "../commons-device.gradle"
apply plugin: 'kotlin-android'

/* gets the version name from the latest Git tag, stripping the leading v off */
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--always'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

static def versionCodeEpoch() {
    return (new Date().getTime() / 1000).toInteger()
}

android {
    defaultConfig {
        applicationId "io.anyone.anyonebot"
        versionName getVersionName()
        multiDexEnabled true
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        archivesBaseName = "AnyoneBot"
    }

    compileOptions {
//        coreLibraryDesugaringEnabled true
    }

    buildFeatures {
        buildConfig = true
    }

    viewBinding {
        enabled = true
    }

    buildTypes {
        release {
            shrinkResources false
            minifyEnabled false
        }
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    flavorDimensions "free"

    productFlavors {
        fullperm {
            dimension "free"
            applicationId 'io.anyone.anyonebot'
            versionCode 4
            versionName getVersionName()
            archivesBaseName = "AnyoneBot-${versionName}"
        }

        nightly {
            dimension "free"
            applicationId 'io.anyone.anyonebot.nightly'
            versionName getVersionName()
            versionCode versionCodeEpoch()
            archivesBaseName = "AnyoneBot-${versionName}"
        }
    }

    splits {
        abi {
            universalApk true
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/androidx.localbroadcastmanager_localbroadcastmanager.version']
        }
    }

    dependencies {
        implementation(
                project(':appcore'),
                project(':intentintegrator'),
                project(':anyonebotservice'),
                "com.google.android.material:material:1.12.0",
                "com.android.volley:volley:1.2.0",
                "androidx.localbroadcastmanager:localbroadcastmanager:1.1.0",
        )

        implementation "com.squareup.retrofit2:retrofit:3.0.0"
        implementation 'com.squareup.retrofit2:converter-gson:3.0.0'

        androidTestImplementation "tools.fastlane:screengrab:2.1.1"
//        coreLibraryDesugaring 'com.android.tools:desugar_j dk_libs:1.1.5'
    }
    lint {
        abortOnError false
        checkReleaseBuilds false
        disable 'InvalidPackage'
        htmlReport true
        lintConfig file('../lint.xml')
        textReport false
        xmlReport false
    }
    namespace 'io.anyone.anyonebot'
}
dependencies {
    implementation "androidx.core:core-ktx:1.17.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    // Kotlin
    implementation "androidx.navigation:navigation-fragment-ktx:2.9.4"
    implementation "androidx.navigation:navigation-ui-ktx:2.9.4"

    //root detection
    implementation 'com.scottyab:rootbeer-lib:0.1.1'

}
