apply from: "../commons-device.gradle"
apply plugin: 'kotlin-android'

/* gets the version name from the latest Git tag, stripping the leading v off */
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--always'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

static def versionCodeEpoch() {
    return (new Date().getTime() / 1000).toInteger()
}

android {
    defaultConfig {
        applicationId "io.anyone.anyonebot"
        versionName getVersionName()
        multiDexEnabled true
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        archivesBaseName = "AnyoneBot"
    }

    compileOptions {
//        coreLibraryDesugaringEnabled true
    }

    buildFeatures {
        buildConfig = true
    }

    viewBinding {
        enabled = true
    }

    buildTypes {
        release {
            shrinkResources false
            minifyEnabled false
        }
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    flavorDimensions "free"

    productFlavors {
        fullperm {
            dimension "free"
            applicationId 'io.anyone.anyonebot'
            versionCode 4
            versionName getVersionName()
            archivesBaseName = "AnyoneBot-${versionName}"
        }

        nightly {
            dimension "free"
            applicationId 'io.anyone.anyonebot.nightly'
            versionName getVersionName()
            versionCode versionCodeEpoch()
            archivesBaseName = "AnyoneBot-${versionName}"
        }
    }

    splits {
        abi {
            universalApk true
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/androidx.localbroadcastmanager_localbroadcastmanager.version']
        }
    }

    ndkVersion = "28.2.13676358"
    externalNativeBuild {
        ndkBuild {
            path = file("src/main/jni/Android.mk")
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

        api "io.anyone:anon-android:0.4.9.11" // project(':anon-android-binary')
        implementation files('../libs/geoip.jar')

        implementation "androidx.appcompat:appcompat:1.7.1"
        implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
        implementation "androidx.core:core:1.17.0"
        implementation 'androidx.core:core-ktx:1.17.0'
        implementation "androidx.localbroadcastmanager:localbroadcastmanager:1.1.0"
        implementation "androidx.navigation:navigation-ui-ktx:2.9.6"
        implementation "androidx.navigation:navigation-fragment-ktx:2.9.6"
        implementation "androidx.preference:preference-ktx:1.2.1"
        implementation "androidx.work:work-runtime:2.11.0"
        implementation "androidx.work:work-runtime-ktx:2.11.0"

        implementation "com.jaredrummler:android-shell:1.0.0"

        implementation 'com.google.android.material:material:1.13.0'
        implementation "com.android.volley:volley:1.2.1"

        implementation "com.squareup.retrofit2:retrofit:3.0.0"
        implementation 'com.squareup.retrofit2:converter-gson:3.0.0'

        //root detection
        implementation 'com.scottyab:rootbeer-lib:0.1.1'

        testImplementation 'junit:junit:4.13.2'
        androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
        androidTestImplementation 'androidx.test.ext:junit:1.3.0'
        androidTestImplementation "tools.fastlane:screengrab:2.1.1"
    }

    lint {
        abortOnError false
        checkReleaseBuilds false
        disable 'InvalidPackage'
        htmlReport true
        lintConfig file('../lint.xml')
        textReport false
        xmlReport false
    }

    namespace 'io.anyone.anyonebot'
}
